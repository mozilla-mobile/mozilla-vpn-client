/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    ext{
        kotlin_version = "1.4.30-M1"
        //  for libwg
        appcompatVersion = '1.1.0'
        annotationsVersion = '1.0.1'
        databindingVersion = '3.3.1'
        jsr305Version = '3.0.2'
        streamsupportVersion = '1.7.0'
        threetenabpVersion = '1.1.1'
        groupName = 'org.mozilla.firefox.vpn'
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}


allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.android.billingclient:billing-ktx:4.0.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0-alpha02"
    implementation "androidx.security:security-crypto:1.1.0-alpha03"
    implementation "androidx.security:security-identity-credential:1.0.0-alpha02"
    implementation 'com.adjust.sdk:adjust-android:4.28.2'
    implementation 'com.google.android.gms:play-services-ads-identifier:17.0.1'
    implementation 'com.google.android.play:core:1.10.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.0.10"
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qt5AndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    compileSdkVersion androidCompileSdkVersion.toInteger()

    buildToolsVersion '28.0.3'

    dexOptions {
       javaMaxHeapSize "3g"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qt5AndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qt5AndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qt5AndroidDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }
    // In case generate a new key anytime:
    // $: keytool -genkey -v -keystore debug.keystore -storepass android \
    //            -alias androiddebugkey -keypass android -keyalg RSA \ 
    //            -keysize 2048 -validity 10000
    signingConfigs {
    debug {
            storeFile rootProject.file('./debug.keystore')
            storePassword('android')
            keyAlias 'androiddebugkey'
            keyPassword('android')
        }
    }

    lintOptions {
        abortOnError false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    buildTypes {
        release {
            // That would enable treeshaking and remove java code that is just called from qt
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${groupName}", "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir}"
                }
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${groupName}.debug", "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir}"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path 'tunnel/CMakeLists.txt'
        }
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode  System.getenv("VERSIONCODE").toInteger()
        versionName  System.getenv("SHORTVERSION")
        buildConfigField "String", "ADJUST_SDK_TOKEN", '"' + System.getenv("ADJUST_SDK_TOKEN") + '"'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
    splits {
                // Configures multiple APKs based on ABI.
                abi {
                    if(System.getenv("SPLITAPK")== "1" ){
                        enable true
                        // Resets the list of ABIs that Gradle should create APKs for to none.
                        reset()

                        // Specifies a list of ABIs that Gradle should create APKs for.
                        include "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
                        // Also generate one universal apk that contains all abi's
                        universalApk true
                    }else{
                        enable false
                    }
                }
            }

}

import com.android.build.OutputFile
// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a':1,"arm64-v8a":2, x86:3, x86_64:4]
android.applicationVariants.all { variant ->
  // Assigns a different version code for each output APK
  // other than the universal APK.
  variant.outputs.each { output ->
    def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
    if (baseAbiVersionCode != null) {
      output.versionCodeOverride =
              baseAbiVersionCode + variant.versionCode
    }
  }
} 
