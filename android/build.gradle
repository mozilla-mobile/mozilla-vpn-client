/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

import com.android.build.OutputFile

buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
    }
    dependencies {
        classpath SharedDependencies.com_android_tools_build_gradle
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.4.30-M1"
    }
}
repositories {
    mavenCentral()
    google()
}



allprojects {
    repositories {
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        mavenCentral()
        google()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':daemon')
    implementation project(path: ':qtBindings')

    implementation SharedDependencies.androidx_core
    implementation "com.android.installreferrer:installreferrer:2.2"
    implementation "com.android.billingclient:billing-ktx:4.0.0"
    implementation "com.google.android.gms:play-services-ads-identifier:17.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

    coreLibraryDesugaring SharedDependencies.com_android_tools_desugar_jdk_libs
}

android {
    compileSdkVersion Config.compileSdkVersion
    buildToolsVersion Config.buildToolsVersion

    dexOptions {
       javaMaxHeapSize "3g"
    }

    sourceSets {
        main {
            java.srcDirs = ['src', 'java']
            aidl.srcDirs = ['src', 'aidl']

            manifest.srcFile 'AndroidManifest.xml'
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs','build/ssl']
       }
        debug{
            try {
                res.srcDirs =  [
                        qtAndroidDir + '/res',
                        'resources/all',
                        'resources/debug',
                        'res'
                ]
            }catch(Exception ignored){
                res.srcDirs = [
                        'resources/all',
                        'resources/debug',
                        'res'
                ]
            }
        }
        release{
            try {
                res.srcDirs =  [
                        qtAndroidDir + '/res',
                        'resources/all',
                        'resources/release',
                        'res'
                ]
            }catch(Exception ignored){
                res.srcDirs = [
                        'resources/all',
                        'resources/release',
                        'res'
                ]
            }
        }
    }

    signingConfigs {
        debug {
            // In case generate a new key anytime:
            // $: keytool -genkey -v -keystore debug.keystore -storepass android \
            //            -alias androiddebugkey -keypass android -keyalg RSA \
            //            -keysize 2048 -validity 10000
            storeFile rootProject.file('./debug.keystore')
            storePassword('android')
            keyAlias 'androiddebugkey'
            keyPassword('android')
        }
    }

    lintOptions {
        abortOnError false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    buildTypes {
        release {
            // That would enable treeshaking and remove java code that is just called from qt
            minifyEnabled false
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            packagingOptions {
                // specify the path to your object binaries, or generally:
                doNotStrip '**.so'
            }
        }
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode  System.getenv("VERSIONCODE")? System.getenv("VERSIONCODE")?.toInteger() : 99999
        versionName  System.getenv("SHORTVERSION") ? System.getenv("SHORTVERSION"): ""
        buildConfigField "String", "ADJUST_SDK_TOKEN", '"' + System.getenv("ADJUST_SDK_TOKEN") + '"'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
    splits {
        abi {
            enable true
            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()
            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
            // Also generate one universal apk that contains all abi's (need this for qmake to pass)
            universalApk true
        }
    }
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a':1,"arm64-v8a":2, x86:3, x86_64:4]
android.applicationVariants.all { variant ->
  // Assigns a different version code for each output APK
  // other than the universal APK.
  variant.outputs.each { output ->
    def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
    if (baseAbiVersionCode != null) {
      output.versionCodeOverride =
              baseAbiVersionCode + variant.versionCode
    }
  }
} 

// Enable Ktlint out of the box via gradle Task
configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest:ktlint:0.45.2") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt", "!**/build/**/*.kt", "buildSrc/**/*.kt",  "daemon/**/*.kt",  "**/*.kts"
}


task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt", "!**/build/**/*.kt", "buildSrc/**/*.kt",  "daemon/**/*.kt", "**/*.kts"
}


// Tasks to Import the Platform plugins
task importOpenSSL(type: Copy) {
    with(copySpec(){
        from("../../../3rdparty/openSSL/latest/arm")
        into("armeabi-v7a")
    })
    with(copySpec(){
        from("../../../3rdparty/openSSL/latest/arm64")
        into("arm64-v8a")
    })
    with(copySpec(){
        from("../../../3rdparty/openSSL/latest/x86")
        into("x86")
    })
    with(copySpec(){
        from("../../../3rdparty/openSSL/latest/x86_64")
        into("x86_64")
    })
    into("build/ssl")
}
preBuild.dependsOn importOpenSSL