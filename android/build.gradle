/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

import com.android.build.OutputFile

buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
    }
    dependencies {
        classpath SharedDependencies.com_android_tools_build_gradle
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.22"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.6.0"
    }
}



repositories {
    mavenCentral()
    google()
}

allprojects {
    repositories {
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        mavenCentral()
        google()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

// Builds the /tunnel go module 
// Produces an .aar file containing the libs 
// and Java glue Code to call it.
task buildTunnelGo(type: Exec) {

    workingDir "$projectDir/tunnel"
    commandLine 'gomobile', 'bind', '-target=android', '-androidapi=24', '-o', "$projectDir/libs/tunnel.aar", '.'
}

// Ensure the Go AAR is built before the preBuild step
preBuild.dependsOn buildTunnelGo


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':qtglean')
    implementation project(path: ':common')
    implementation project(path: ':qtBindings')

    coreLibraryDesugaring SharedDependencies.com_android_tools_desugar_jdk_libs

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation SharedDependencies.androidx_core
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.22"
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1")
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.22"
    implementation "com.android.installreferrer:installreferrer:2.2"
    implementation "com.android.billingclient:billing:7.0.0"
    implementation "com.google.android.gms:play-services-ads-identifier:17.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"


}

android {
    ndkVersion Config.ndkVersion
    compileSdkVersion Config.compileSdkVersion
    buildToolsVersion Config.buildToolsVersion

    dexOptions {
       javaMaxHeapSize "3g"
    }

    sourceSets {
        main {
            java.srcDirs = ['src', 'java']
            aidl.srcDirs = ['src', 'aidl']
            manifest.srcFile 'AndroidManifest.xml'

            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs','build/ssl']


            kotlin.srcDirs = [
                "${System.getProperty('user.dir')}/../../clienttelemetry",
            ]
        
        }
        debug{
            try {
                res.srcDirs =  [
                        qtAndroidDir + '/res',
                        'resources/all',
                        'resources/debug',
                        'res'
                ]
            }catch(Exception ignored){
                res.srcDirs = [
                        'resources/all',
                        'resources/debug',
                        'res'
                ]
            }
        }
        release{
            try {
                res.srcDirs =  [
                        qtAndroidDir + '/res',
                        'resources/all',
                        'resources/release',
                        'res'
                ]
            }catch(Exception ignored){
                res.srcDirs = [
                        'resources/all',
                        'resources/release',
                        'res'
                ]
            }
        }
    }

    signingConfigs {
        debug {
            // In case generate a new key anytime:
            // $: keytool -genkey -v -keystore debug.keystore -storepass android \
            //            -alias androiddebugkey -keypass android -keyalg RSA \
            //            -keysize 2048 -validity 10000
            storeFile rootProject.file('./debug.keystore')
            storePassword('android')
            keyAlias 'androiddebugkey'
            keyPassword('android')
        }
    }

    lintOptions {
        abortOnError false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    buildTypes {
        release {
            // That would enable treeshaking and remove java code that is just called from qt
            minifyEnabled false
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            packagingOptions {
                // specify the path to your object binaries, or generally:
                doNotStrip '**.so'
            }
        }
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode  System.getenv("VERSIONCODE")? System.getenv("VERSIONCODE")?.toInteger() : 99999
        versionName  System.getenv("SHORTVERSION") ? System.getenv("SHORTVERSION"): ""

        buildConfigField "int", "VERSION_CODE", System.getenv("VERSIONCODE") ?: "99999"
        buildConfigField "String", "VERSION_NAME", '"' + (System.getenv("SHORTVERSION") ?: "") + '"'
        buildConfigField  "String", "VERSIONCODE" ,  '"' +System.getenv("VERSIONCODE") + '"'
        buildConfigField  "String", "SHORTVERSION" ,  '"' + System.getenv("SHORTVERSION") +  '"'
        buildConfigField  "String", "ADJUST_SDK_TOKEN" ,  '"' + (project.properties["adjusttoken"] ?: "no_token_found") +  '"'


    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
    splits {
        abi {
            enable true
            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()
            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
            // Also generate one universal apk that contains all abi's (need this for qmake to pass)
            universalApk true
        }
    }
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a':1,"arm64-v8a":2, x86:3, x86_64:4]
android.applicationVariants.all { variant ->
  // Assigns a different version code for each output APK
  // other than the universal APK.
  variant.outputs.each { output ->
    def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
    if (baseAbiVersionCode != null) {
      output.versionCodeOverride =
              baseAbiVersionCode + variant.versionCode
    }
  }
}
