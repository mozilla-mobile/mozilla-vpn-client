buildscript {
    repositories {
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        google()
        mavenCentral()

    }
    ext{
        groupName = 'org.mozilla.firefox.vpn'
        try{
            if(qtAndroidDir){
                print("Android Location for wg")
            }
            WG_TOOLS_DIR= "../../../../../3rdparty/wireguard-tools"
        }catch(Exception ignored){
            // This is checked from <proj>/android/daemon/tunnel
            WG_TOOLS_DIR= "../../../3rdparty/wireguard-tools"

        }

    }

    dependencies {
        classpath Dependencies.com_android_tools_build_gradle
        classpath Dependencies.org_mozilla_components_tooling_glean_gradle
    }
}

plugins {
    id "com.jetbrains.python.envs" version "0.0.26"
}


apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "org.mozilla.telemetry.glean-gradle-plugin"


android {
    compileSdkVersion Config.compileSdkVersion
    ndkVersion Config.ndkVersion
    defaultConfig {
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        buildConfig = true
    }
    sourceSets {
        main {
            res.srcDirs =  ["res"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path 'tunnel/CMakeLists.txt'
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${groupName}.debug",
                              "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir},",
                              "-DCMAKE_WG_TOOLS_DIR=${WG_TOOLS_DIR}"
                }
            }
        }
        release {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${groupName}",
                              "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir}",
                              "-DCMAKE_WG_TOOLS_DIR=${WG_TOOLS_DIR}"
                }
            }
        }
    }


}

dependencies {

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation Dependencies.androidx_core
    implementation Dependencies.android_installreferrer
    implementation Dependencies.android_billingclient
    implementation Dependencies.androidx_lifecycle
    implementation Dependencies.androidx_security_security_crypto
    implementation Dependencies.androidx_security_security_identity_credential
    implementation Dependencies.com_google_android_gms_play_services_ads_identifier
    implementation Dependencies.org_jetbrains_kotlinx_kotlinx_serialization_json
    implementation Dependencies.org_mozilla_telemetry_glean
    coreLibraryDesugaring Dependencies.com_android_tools_desugar_jdk_libs
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.6.10"
}

ext.gleanNamespace = "mozilla.telemetry.glean"
try{
    if(qtAndroidDir){}
    // We're reunning gradle via qmake in .tmp/src/android-build
    ext.gleanYamlFiles = ["$rootDir/../../../glean/metrics.yaml", "$rootDir/../../../glean/pings.yaml"]
}catch(Exception ignored){
    // We running gradle from /android
    ext.gleanYamlFiles = ["$rootDir/../glean/metrics.yaml", "$rootDir/../glean/pings.yaml"]
}