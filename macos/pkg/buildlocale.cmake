#! /usr/bin/env python3
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.

## Gather translations from the locale's XLIFF file.
function(get_translation TRID)
    string(REPLACE "." "_" TRID_SNAKE ${TRID})
    string(TOUPPER ${TRID_SNAKE} TRID_VARNAME)

    execute_process(
        OUTPUT_VARIABLE TRID_RESULT
        OUTPUT_STRIP_TRAILING_WHITESPACE
        WORKING_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/../../i18n/${LOCALE}
        COMMAND ${CMAKE_COMMAND} -E echo $(pwd)
        COMMAND ${CMAKE_CURRENT_LIST_DIR}/../../scripts/utils/xlifftool.py -g ${TRID} mozillavpn.xliff)

    set(${TRID_VARNAME} "${TRID_RESULT}" PARENT_SCOPE)
    set(MVPN_OSXINSTALL_VARNAMES ${MVPN_OSXINSTALL_VARNAMES} ${TRID_VARNAME} PARENT_SCOPE)
endfunction()

get_translation(macosinstaller.title)
get_translation(macosinstaller.unsupported_version.title)
get_translation(macosinstaller.unsupported_version.message)
get_translation(macosinstaller.previous_build.title)
get_translation(macosinstaller.previous_build.message)
get_translation(macosinstaller.welcome.message1)
get_translation(macosinstaller.welcome.message2)
get_translation(macosinstaller.conclusion.title)
get_translation(macosinstaller.conclusion.message1_v2)
get_translation(macosinstaller.conclusion.message2)
get_translation(macosinstaller.conclusion.message3)
set(MACOSINSTALLER_CONCLUSION_URL "https://support.mozilla.org/products/firefox-private-network-vpn?utm_source=mozilla-vpn&utm_medium=mozilla-vpn-installer&utm_campaign=mac-installer")

file(MAKE_DIRECTORY ${LOCALE}.lproj)
configure_file(${CMAKE_CURRENT_LIST_DIR}/welcome.html.in ${LOCALE}.lproj/welcome.html)
configure_file(${CMAKE_CURRENT_LIST_DIR}/conclusion.html.in ${LOCALE}.lproj/conclusion.html)

file(WRITE ${LOCALE}.lproj/Localizable.strings "/* AUTOGENERATED! DO NOT EDIT!! */\n")
foreach(TRID_VARNAME ${MVPN_OSXINSTALL_VARNAMES})
    string(SUBSTRING ${TRID_VARNAME} 15 -1 TRID_TRUNCATE)
    string(TOLOWER ${TRID_TRUNCATE} TRID_STRINGNAME)
    file(APPEND ${LOCALE}.lproj/Localizable.strings "'${TRID_STRINGNAME}' = '${${TRID_VARNAME}}';\n")
endforeach()
file(APPEND ${LOCALE}.lproj/Localizable.strings "\n")
